!########################################################
!     Program  : HMIPT
!     PURPOSE  : Solve the Hubbard model using DMFT-IPT
!     AUTHORS  : Adriano Amaricci
!########################################################
program hmipt_zerot_keldysh
  USE IPT_VARS_GLOBAL
  implicit none

  integer                :: i,Lk,ikm
  logical                :: converged
  complex(8)             :: zeta,sqroot
  real(8)                :: sq,sig
  complex(8),allocatable :: fg(:),fg0(:),sigma(:)
  complex(8),allocatable :: fg0t(:),sigmat(:)
  real(8),allocatable    :: wr(:),t(:)
  type(keldysh_equilibrium_gf) :: fgk0,sigmak

  call read_input("inputIPT.in")
  allocate(fg(2*L),fg0(2*L),sigma(2*L))
  allocate(fg0t(-L:L),sigmat(-L:L))


  call allocate_gf(fgk0,L)
  call allocate_gf(sigmak,L)

  !grids:
  allocate(wr(2*L))
  wr = linspace(-wmax,wmax,2*L,mesh=fmesh)
  dt = pi/wmax
  allocate(t(-L:L))  
  t  = linspace(-pi/wmax,pi/wmax,2*L+1)


  D=1.d0 ; sigmak=zero ; iloop=0 ; converged=.false.
  do while (.not.converged)
     iloop=iloop+1
     write(*,"(A,i5,1x)",advance="no")"DMFT-loop",iloop
     do i=1,2*L
        zeta  = cmplx(wr(i),eps) + xmu - sigmak%ret%w(i)
        fg(i)    = gfbether(wr(i),zeta,D)
     enddo

     fgk0%ret%w =one/(one/fg + sigmak%ret%w)
     fgk0%less%w=less_component_w(fg,wr,beta)
     fgk0%gtr%w =gtr_component_w(fg,wr,beta)
     call fftgf_rw2rt(fgk0%less%w,fgk0%less%t,L); fgk0%less%t=xi*fmesh*fgk0%less%t
     call fftgf_rw2rt(fgk0%gtr%w ,fgk0%gtr%t ,L); fgk0%gtr%t =xi*fmesh*fgk0%gtr%t
     do i=-L,L
        sigmak%less%t(i)=(U**2)*(fgk0%less%t(i)**2)*fgk0%gtr%t(-i)
        sigmak%gtr%t(i) =(U**2)*(fgk0%gtr%t(i)**2)*fgk0%less%t(-i)
     enddo
     sigmak%ret%t=ret_component_t(sigmak%gtr%t,sigmak%less%t,t,L)
     call fftgf_rt2rw(sigmak%ret%t,sigmak%ret%w,L); sigmak%ret%w= dt*sigmak%ret%w

     converged= check_convergence(sigmak%ret%w,eps_error,nsuccess,nloop)
  enddo
  call splot("DOS.ipt",wr,-aimag(fg)/pi,append=printf)
  call splot("Sigma_realw.ipt",wr,sigmak%ret%w,append=printf)
  call splot("G0_realw.ipt",wr,fgk0%ret%w,append=printf)


end program hmipt_zerot_keldysh

